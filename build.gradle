buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'idea'

mixin {
    add sourceSets.main, "${mod_name.toLowerCase()}.refmap.json"
}

final def lib = project(':_Lollipop')

version = lib.minecraft_version + '-' + mod_version
group = lib.mod_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: lib.mappings_channel, version: lib.mappings_version
    accessTransformer = file('../Lollipop/src/main/resources/META-INF/accesstransformer.cfg')
}

tasks.withType(Jar) {
    def out = file("../_JARS")
    destinationDir = out
}

repositories {
    maven { url = "http://dvs1.progwml6.com/files/maven" } //JEI
    maven { url = "https://maven.theillusivec4.top" } //Curios
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + lib.minecraft_version + '-' + lib.forge_version
    compileOnly fg.deobf("mezz.jei:jei-${lib.jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${lib.jei_version}")
    compile fg.deobf("top.theillusivec4.curios:curios-forge:${lib.curios_version}")
    annotationProcessor 'org.spongepowered:mixin:0.8.1:processor'
    compile lib
}

jar {
    manifest {
        attributes([
                "MixinConfigs"            : "${mod_name.toLowerCase()}.mixins.json",
                'FMLAT'                   : 'accesstransformer.cfg',
                "Specification-Title"     : mod_name,
                "Specification-Vendor"    : lib.mod_group,
                "Specification-Version"   : mod_version,
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : lib.mod_group,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ],)
    }
}

if (System.getProperty("idea.sync.active") == "true") {
    afterEvaluate {
        tasks.withType(JavaCompile).all {
            it.options.annotationProcessorPath = files()
        }
    }
}